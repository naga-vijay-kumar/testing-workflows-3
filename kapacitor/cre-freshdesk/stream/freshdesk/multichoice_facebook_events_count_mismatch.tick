var incoming = stream
  |from()
    .database('cre-freshdesk')
    .measurement('cre_cf_sherlock_incoming_facebook_events_total')
  |where(lambda: "account_id" == '986130')
  |derivative('counter')
    .unit(10m)
    .nonNegative()
    .as('incomingEvents')
  |window()
    .period(5m)
    .every(5m)
  |mean('incomingEvents')
    .as('incomingEvents')


var converted = stream
  |from()
    .database('cre-freshdesk')
    .measurement('cre_cf_sherlock_incoming_helpkit_trends_total')
  |where(lambda: "account_id" == '986130' AND "source" == 'facebook')
  |derivative('counter')
    .unit(10m)
    .nonNegative()
    .as('convertedEvents')
  |window()
    .period(5m)
    .every(5m)
  |mean('convertedEvents')
    .as('convertedEvents')


incoming
  |join(converted)
    .as('incoming', 'converted')
    .tolerance(1s)
    .fill(0.0)
    .streamName('difference')
  |eval(lambda: "incoming.value" - "converted.value")
    .as('difference')
  |stateDuration(lambda: "difference" >= 20)
    .unit(1m)
  |alert()
    .message('[ {{ .Time }} ] [multichoice_facebook_events_count_mismatch] [ The difference in number of incoming facebook events and actual tickets/notes created in FD is above 20 for duration ] {{ index .Fields "state_duration" }} {{ .Level }}')
    .warn(lambda: "state_duration" >= 10)
    .warnReset(lambda: "state_duration" == 0)
    .stateChangesOnly()
    .topic('cre-freshdesk')